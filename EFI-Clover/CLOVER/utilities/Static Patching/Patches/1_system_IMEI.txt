# Lenovo ThinkPad T450 macOS Mojave (Hackintosh Build By James Sassu)
#
# This patch adds the IMEI device in the DSDT configuration file...
#
# Device IMEI will be added to SB.PCI0 
#
into device label IMEI parent_label PCI0 remove_entry;
into device label PCI0 insert
begin
Device (IMEI)\n
{\n
    Name (_ADR, 0x00160000)\n
	Method (_DSM, 4, NotSerialized)\n
	{\n
		Store (Package () {\n
			"AAPL,slot-name", Buffer() {"Built In"},\n
			"name", Buffer() {"Intel MEI Controller"},\n
			"model", Buffer() {"Intel Broadwell Wildcat Point-LP MEI Controller"},\n
			"device_type", Buffer() {"Communication Controller"},\n
			"device-id", Buffer() {0xBA, 0x9C, 0x00, 0x00},\n
			"vendor-id", Buffer() {0x86, 0x80, 0x00, 0x00},\n
			"compatible", Buffer() {"pci8086,9cba"},\n
			"built-in", Buffer() {0x01},\n
		}, Local0)\n
		DTGP (Arg0, Arg1, Arg2, Arg3, RefOf (Local0))\n
		Return (Local0)\n
	}\n
}
end;
#
# Insert DTGP (Specific to Apple authentic hardware, helps legitimize code present in DSDT)
#
into method label DTGP remove_entry;
into definitionblock code_regex . insert
begin
Method (DTGP, 5, NotSerialized)\n
{\n
    If (LEqual (Arg0, Buffer (0x10)\n
            {\n
                /* 0000 */    0xC6, 0xB7, 0xB5, 0xA0, 0x18, 0x13, 0x1C, 0x44, \n
                /* 0008 */    0xB0, 0xC9, 0xFE, 0x69, 0x5E, 0xAF, 0x94, 0x9B\n
            }))\n
    {\n
        If (LEqual (Arg1, One))\n
        {\n
            If (LEqual (Arg2, Zero))\n
            {\n
                Store (Buffer (One)\n
                    {\n
                        0x03\n
                    }, Arg4)\n
                Return (One)\n
            }\n
            If (LEqual (Arg2, One))\n
            {\n
                Return (One)\n
            }\n
        }\n
    }\n
    Store (Buffer (One)\n
        {\n
            0x00\n
        }, Arg4)\n
    Return (Zero)\n
}
end;

